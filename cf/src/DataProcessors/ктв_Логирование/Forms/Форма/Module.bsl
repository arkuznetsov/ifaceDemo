
// Функция получает и устанавливает текущее имя лога
//
// Параметры:
//	НовоеИмяЛога			- Строка		- Новое имя лога
//
// Возвращаемое значение:
//	Строка		- Текущее имя лога
//
&НаКлиенте
Функция ИмяЛога(Знач НовоеИмяЛога = Неопределено) Экспорт
	
	ТекИмяЛога = Объект.ИмяЛога;
	
	Если ТипЗнч(НовоеИмяЛога) = Тип("Строка") Тогда
		Объект.ИмяЛога = НовоеИмяЛога;
	КонецЕсли;
	
	Возврат ТекИмяЛога;

КонецФункции //ИмяЛога()

// Функция получает и устанавливает параметры записи лога
//
// Параметры:
//	НовыеПараметрыЗаписи			- Структура		- Новые параметры записи лога
//
// Возвращаемое значение:
//	Строка		- Текущие параметры записи лога
//
&НаКлиенте
Функция ПараметрыЗаписи(Знач НовыеПараметрыЗаписи = Неопределено) Экспорт
	
	Если НЕ ТипЗнч(Объект.ПараметрыЗаписи) = Тип("Структура") Тогда
		Объект.ПараметрыЗаписи = Новый Структура();
	КонецЕсли;
	
	ТекПараметры = Объект.ПараметрыЗаписи;
	
	Если ТипЗнч(НовыеПараметрыЗаписи) = Тип("Структура") Тогда
		Объект.ПараметрыЗаписи = Новый Структура();
		Для Каждого ТекЭлемент Из НовыеПараметрыЗаписи Цикл
			Объект.ПараметрыЗаписи.Вставить(ТекЭлемент.Ключ, ТекЭлемент.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТекПараметры;

КонецФункции //ПараметрыЗаписи()

// Процедура выполняет запись в лог на клиенте
//
// Параметры:
//	Текст					- Строка					- текст выводимого сообщения
//	УровеньЛога				- Число						- уровень лога: Отладка, Информация, ошибка и т.п.
//	ПараметрыЗаписи			- Структура					- дополнительные параметры записи информации в лог
//
&НаКлиенте
Процедура ЗаписатьВЛог(Текст, УровеньЛога = 1, ПараметрыЗаписи = Неопределено) Экспорт
	
	Если НЕ ТипЗнч(ПараметрыЗаписи) = Тип("Структура") Тогда
		ПараметрыЗаписи = Новый Структура();
	КонецЕсли;
	
	ВсеПараметрыЗаписи = Новый Структура();
	
	// Добавляем параметры записи лога, заданные для обработки вцелом
	Для Каждого ТекПараметр Из ПараметрыЗаписи() Цикл
		ВсеПараметрыЗаписи.Вставить(ТекПараметр.Ключ, ТекПараметр.Значение);
	КонецЦикла;
	
	// Добавляем параметры записи лога, переданные при вызове процедуры
	Для Каждого ТекПараметр Из ПараметрыЗаписи Цикл
		ВсеПараметрыЗаписи.Вставить(ТекПараметр.Ключ, ТекПараметр.Значение);
	КонецЦикла;
	
	ктв_Логирование.ЗаписатьВЛог(ИмяЛога(), Текст, УровеньЛога, ВсеПараметрыЗаписи);
	
	// Возвращаем изменения параметров записи
	Для Каждого ТекПараметр Из ВсеПараметрыЗаписи Цикл
		ПараметрыЗаписи.Вставить(ТекПараметр.Ключ, ТекПараметр.Значение);
	КонецЦикла;
	
КонецПроцедуры //ЗаписатьВЛог()

