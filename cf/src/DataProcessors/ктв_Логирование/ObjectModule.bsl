
// Функция получает и устанавливает текущее имя лога
//
// Параметры:
//	НовоеИмяЛога			- Строка		- Новое имя лога
//
// Возвращаемое значение:
//	Строка		- Текущее имя лога
//
Функция ИмяЛога(Знач НовоеИмяЛога = Неопределено) Экспорт
	
	ТекИмяЛога = ЭтотОбъект.ИмяЛога;
	
	Если ТипЗнч(НовоеИмяЛога) = Тип("Строка") Тогда
		ЭтотОбъект.ИмяЛога = НовоеИмяЛога;
	КонецЕсли;
	
	Возврат ТекИмяЛога;

КонецФункции //ИмяЛога()

// Функция получает и устанавливает параметры записи лога
//
// Параметры:
//	НовыеПараметрыЗаписи			- Структура		- Новые параметры записи лога
//
// Возвращаемое значение:
//	Структура		- Текущие параметры записи лога
//
Функция ПараметрыЗаписи(Знач НовыеПараметрыЗаписи = Неопределено) Экспорт
	
	Если НЕ ТипЗнч(ЭтотОбъект.ПараметрыЗаписи) = Тип("Структура") Тогда
		ЭтотОбъект.ПараметрыЗаписи = Новый Структура();
	КонецЕсли;
	
	ТекПараметры = ЭтотОбъект.ПараметрыЗаписи;
	
	Если ТипЗнч(НовыеПараметрыЗаписи) = Тип("Структура") Тогда
		ЭтотОбъект.ПараметрыЗаписи = Новый Структура();
		Для Каждого ТекЭлемент Из НовыеПараметрыЗаписи Цикл
			ЭтотОбъект.ПараметрыЗаписи.Вставить(ТекЭлемент.Ключ, ТекЭлемент.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТекПараметры;

КонецФункции //ПараметрыЗаписи()

// Процедура выполняет запись в лог на сервере
//
// Параметры:
//	Текст					- Строка					- текст выводимого сообщения
//	УровеньЛога				- Число						- уровень лога: Отладка, Информация, ошибка и т.п.
//	ПеремПараметрыЗаписи	- Структура					- дополнительные параметры записи информации в лог
//
Процедура ЗаписатьВЛог(Текст, УровеньЛога = 1, ПеремПараметрыЗаписи = Неопределено) Экспорт
	
	Если НЕ ТипЗнч(ПеремПараметрыЗаписи) = Тип("Структура") Тогда
		ПеремПараметрыЗаписи = Новый Структура();
	КонецЕсли;
	
	ВсеПараметрыЗаписи = Новый Структура();
	
	// Добавляем параметры записи лога, заданные для обработки вцелом
	Для Каждого ТекПараметр Из ПараметрыЗаписи() Цикл
		ВсеПараметрыЗаписи.Вставить(ТекПараметр.Ключ, ТекПараметр.Значение);
	КонецЦикла;
	
	// Добавляем параметры записи лога, переданные при вызове процедуры
	Для Каждого ТекПараметр Из ПеремПараметрыЗаписи Цикл
		ВсеПараметрыЗаписи.Вставить(ТекПараметр.Ключ, ТекПараметр.Значение);
	КонецЦикла;
	
	ктв_Логирование.ЗаписатьВЛог(ИмяЛога(), Текст, УровеньЛога, ВсеПараметрыЗаписи);
	
	// Возвращаем изменения параметров записи
	Для Каждого ТекПараметр Из ВсеПараметрыЗаписи Цикл
		ПеремПараметрыЗаписи.Вставить(ТекПараметр.Ключ, ТекПараметр.Значение);
	КонецЦикла;
	
КонецПроцедуры //ЗаписатьВЛог()
