////////////////////////////////////////////////////////////////////////////////
// Подсистема "Логирование". (Основной модуль)
//
////////////////////////////////////////////////////////////////////////////////


#Область СлужебныеПроцедуры

// Функция - получает массив способов логирования, для указанного имени лога
//
// Параметры:
//	ИмяЛога					- Строка					- имя лога
//
// Возвращаемое значение:
//	Массив (СправочникСсылка.ктв_СпособыЛогирования)	- массив способов логирования, соответствующих имени лога
//
Функция ПолучитьСпособыЛогирования(ИмяЛога) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	                      |	ИспользованиеИменЛогов.СпособЛогирования КАК СпособЛогирования
	                      |ИЗ
	                      |	РегистрСведений.ктв_ИспользованиеИменЛогов КАК ИспользованиеИменЛогов
	                      |ГДЕ
	                      |	ИспользованиеИменЛогов.ИмяЛога = &ИмяЛога
	                      |	ИЛИ ИспользованиеИменЛогов.СпособЛогирования.ИспользоватьДляВсех");
	
	Запрос.УстановитьПараметр("ИмяЛога", ИмяЛога);
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить().ВыгрузитьКолонку("СпособЛогирования");
	
КонецФункции //ПолучитьСпособыЛогирования()

// Функция - получает список доступных имен логов
//
// Параметры:
//	Исключения		- Массив (Строка)		- массив имен логов, исключаемых из результата
//
// Возвращаемое значение:
//	Массив (Строка)			- массив доступных имен логов
//
Функция ПолучитьИменаЛогов(Знач Исключения) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	ИспользованиеИменЛогов.ИмяЛога КАК ИмяЛога
	                      |ИЗ
	                      |	РегистрСведений.ктв_ИспользованиеИменЛогов КАК ИспользованиеИменЛогов
	                      |ГДЕ
	                      |	НЕ ИспользованиеИменЛогов.ИмяЛога В (&Исключения)
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ИмяЛога");
	
	Если НЕ ТипЗнч(Исключения) = Тип("Массив") Тогда
		Исключения = Новый Массив();
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Исключения", Исключения);
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить().ВыгрузитьКолонку("ИмяЛога");
	
КонецФункции //ПолучитьИменаЛогов()

#КонецОбласти 

#Область ОбработкаЛогирования

// Процедура выполняет запись в лог, с использованием указанного способа логирования
//
// Параметры:
//	Текст					- Строка					- текст выводимого сообщения
//	СпособЛогирования		- СправочникСсылка.			- используемый способ вывода лога
//							  ктв_СпособыЛогирования
//	ПараметрыЗаписи			- Структура					- дополнительные параметры записи информации в лог
//
Процедура ЗаписатьВЛог(Текст, СпособЛогирования, ПараметрыЗаписи) Экспорт
	
	ВремОбработка =
		ктв_ОбработкиОбслуживанияВызовСервера.ПолучитьАдресОбработкиОбслуживанияЭлемента(СпособЛогирования,
																						 "Обработка");
	Если НЕ ктв_ОбработкиОбслуживанияВызовСервера.ЭтоОбработкаОбслуживания(ВремОбработка, "ВыводитьНаСервере") Тогда
		Возврат;
	КонецЕсли;
	
	ВремОбработка = ктв_ОбработкиОбслуживанияВызовСервера.ПолучитьАдресОбработкиОбслуживанияЭлемента(СпособЛогирования, "Обработка");
	
	Обработчик = ктв_ОбработкиОбслуживания.ПолучитьОбработкуОбъект(ВремОбработка);
		
	ТекстОшибки = "";
	
	Попытка
		
		Обработчик.ЗаписатьВЛог(Текст, СпособЛогирования, ПараметрыЗаписи);
		
		Если ПараметрыЗаписи.Свойство("ТекстОшибки") Тогда
			ТекстОшибки = ПараметрыЗаписи.ТекстОшибки;
		КонецЕсли;
		
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если ПараметрыЗаписи.Свойство("ТекстОшибки") Тогда
			ПараметрыЗаписи.ТекстОшибки = ПараметрыЗаписи.ТекстОшибки + Символы.ПС + ТекстОшибки;
		Иначе
			ПараметрыЗаписи.Вставить("ТекстОшибки", ТекстОшибки);
		КонецЕсли;
	КонецПопытки;
	
КонецПроцедуры //ЗаписатьВЛог()

#КонецОбласти

