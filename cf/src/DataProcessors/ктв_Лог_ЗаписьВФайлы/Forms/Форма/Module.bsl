
// Процедура - обработчик события ПриСозданииНаСервере
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ОбработкаНастройкиАдресВХранилище") Тогда
		ОбработкаНастройкиАдресВХранилище = Параметры.ОбработкаНастройкиАдресВХранилище;
	
		Настройки = ПолучитьИзВременногоХранилища(ОбработкаНастройкиАдресВХранилище);
	
		Если ТипЗнч(Настройки) = Тип("Структура") Тогда
			
			Если Настройки.Свойство("КаталогЛогирования") Тогда
				КаталогЛогирования = Настройки.КаталогЛогирования;
			КонецЕсли;
			
			Если Настройки.Свойство("МаксРазмерФайла") Тогда
				МаксРазмерФайла = Настройки.МаксРазмерФайла;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

#Область ОбработчикиКомандФормы

// Процедура - обработчик команды СохранитьНастройку
//
&НаКлиенте
Процедура СохранитьНастройку(Команда)
	
	СохранитьНастройкуНаСервере();
	
	Оповестить("ИзмененыНастройки", ЭтаФорма.ВладелецФормы, ЭтаФорма);
	
	Закрыть("ИзмененыНастройки");
	
КонецПроцедуры //СохранитьНастройку()

// Процедура - обработчик команды СохранитьНастройку
//
&НаСервере
Процедура СохранитьНастройкуНаСервере()
	
	Настройка = Новый Структура();
	
	Настройка.Вставить("КаталогЛогирования",	КаталогЛогирования);
	Настройка.Вставить("МаксРазмерФайла",		МаксРазмерФайла);
	
	ПоместитьВоВременноеХранилище(Настройка, ОбработкаНастройкиАдресВХранилище);
	
КонецПроцедуры //СохранитьНастройкуНаСервере()

// Процедура - обработчик события НачалоВыбора поля КаталогЛогирования 
//
&НаКлиенте
Процедура КаталогЛогированияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Заголовок = "Каталог логирования";

	ОбработкаВыбораКаталога = Новый ОписаниеОповещения("КаталогЛогированияНачалоВыбораЗавершение", ЭтаФорма);
	
	Диалог.Показать(ОбработкаВыбораКаталога);
	
КонецПроцедуры // КаталогЛогированияНачалоВыбора()

// Процедура - продолжение обработчика события НачалоВыбора поля КаталогЛогирования 
//
&НаКлиенте
Процедура КаталогЛогированияНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КаталогЛогирования = ВыбранныеФайлы[0];
	
КонецПроцедуры // КаталогЛогированияНачалоВыбораЗавершение()

// Процедура - обработчик события Открытие поля КаталогЛогирования 
//
&НаКлиенте
Процедура КаталогЛогированияОткрытие(Элемент, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ОбработкаОткрытия = Новый ОписаниеОповещения("КаталогЛогированияОткрытиеЗавершение", ЭтаФорма);
	
	НачатьЗапускПриложения(ОбработкаОткрытия, КаталогЛогирования, , Истина);
	
КонецПроцедуры // КаталогЛогированияОткрытие()

// Процедура - продорлжение обработчика события Открытие поля КаталогЛогирования 
//
&НаКлиенте
Процедура КаталогЛогированияОткрытиеЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры // КаталогЛогированияОткрытиеЗавершение()

#КонецОбласти

